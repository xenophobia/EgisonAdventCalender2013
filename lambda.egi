(define $term
  (algebraic-data-matcher
    {<var string>
     <lam string term>
     <app term term>}))

(define $value
  (pattern-function [$p] (& (| <var _> <lam _ _>) p)))

(define $subst
  (lambda [$x $t]
    (letrec {[$go
              (match-lambda term
                {[<var ,x> t]
                 [<lam (& $y ^,x) $t1> <Lam y (go t1)>]
                 [<app $t1 $t2> <App (go t1) (go t2)>]
                 [$t t]})]} go)))

(define $beta-reduce-cbv-naive
  (match-lambda term
    {[<app <lam $x $t> (value $v)> ((subst x v) t)]
     [<app (value $v) $t> <App v (beta-reduce-cbv-naive t)>]
     [<app $t1 $t2> <App (beta-reduce-cbv-naive t1) t2>]
     [$v v]}))

(define $beta-reduce-cbn-naive
  (match-lambda term
    {[<app <lam $x $t1> $t2> ((subst x t2) t1)]
     [<app $t1 $t2> <App (beta-reduce-cbn-naive t1) t2>]
     [$v v]}))

(define $redex
  (pattern-function []
    <app <lam _ _> _>))

(define $sub
  (pattern-function [$p]
    (| <lam _ (sub p)> <app (sub p) _> <app _ (sub p)> p)))

(define $beta-reduce-normal-naive
  (match-lambda term
    {[<app <lam $x $t1> $t2> ((subst x t2) t1)]
     [<app (& (sub (redex)) $t1) $t2> <App (beta-reduce-normal-naive t1) t2>]
     [<app $t1 (& (sub (redex)) $t2)> <App t1 (beta-reduce-normal-naive t2)>]
     [<lam $x (& (sub (redex)) $t)> <Lam x (beta-reduce-normal-naive t)>]
     [$v v]}))

(define $n-step
  (lambda [$f $t $n]
    (if (lte? n 0)
        t
        (n-step f (f t) (- n 1)))))

(define $succ-term
  <Lam "n"
       <Lam "f"
            <Lam "x"
                 <App <Var "f">
                      <App <App <Var "n"> <Var "f">>
                           <Var "x">>>>>>)

(define $c0 <Lam "f" <Lam "x" <Var "x">>>)

(define $omega <App <Lam "x" <App <Var "x"> <Var "x">>>
                    <Lam "x" <App <Var "x"> <Var "x">>>>)